plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services' // adding for firebase
    id 'kotlin-kapt' // for hilt
    id 'dagger.hilt.android.plugin' // for hilt
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.maverkick.app"
        minSdk 26
        targetSdk 33
        versionCode 3
        versionName "1.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        viewBinding true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation project(':common')
    implementation project(':data')
    implementation project(':student')
    implementation project(':teacher')
    implementation project(':auth')

    implementation "androidx.core:core-ktx:$rootProject.coreKtx"
    implementation "androidx.appcompat:appcompat:$rootProject.appcompat"
    implementation "com.google.android.material:material:$rootProject.material"
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.constraintlayout"
    testImplementation "junit:junit:$rootProject.junit"
    
    // Hilt DI
    implementation "com.google.dagger:hilt-android:$rootProject.hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$rootProject.hilt_version"

    // Hilt WorkManager
    implementation "androidx.hilt:hilt-work:$rootProject.hilt_work_version"
    kapt 'androidx.hilt:hilt-compiler:1.0.0'

    // WorkManager
    implementation "androidx.work:work-runtime-ktx:$rootProject.work_version"

    // for firebase
    implementation "com.google.firebase:firebase-auth-ktx:$rootProject.firebaseAuth"
    implementation 'com.google.firebase:firebase-storage-ktx:20.2.1'

    // for view model
    implementation "androidx.navigation:navigation-fragment-ktx:$rootProject.navigation"

    //  for shared preferences encryption
    implementation 'androidx.security:security-crypto:1.1.0-alpha06'

    // For Retrofit (send the request to the api service)
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofit"
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'

    // for database search (Algolia)
    implementation "com.algolia:algoliasearch-client-kotlin:2.0.0"
}

kapt {
    correctErrorTypes true
}
configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'org.jetbrains.kotlin') {
            details.useVersion '1.8.10'
        }
    }
}